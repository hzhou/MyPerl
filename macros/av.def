subcode:: sv_prerequisite
    $struct(AV) struct SV** p_array, i_0, i_n, size
    $register_prefix(av) struct AV *

macros: 
    SV_UNION:: struct AV A
    SV_TYPES:: SVt_av

    sv_av: &($1->value.A)
#---------------------------------------- 
subcode: with_sv_av(sv)
    $(set:list=$(sv)->value.A.p_array)
    $(set:i_0=$(sv)->value.A.i_0)
    $(set:i_n=$(sv)->value.A.i_n)
    $(set:size=$(sv)->value.A.size)
    p_sv = $(list)+$(i_0)
    n_len  = $(i_n)-$(i_0)
    BLOCK

subcode: sv_set_av(sv, list, i_0, i_n, size)
    $(set:av=$(sv)->value.A)
    $(av).p_array=$(list)
    $(av).i_0 = $(i_0)
    $(av).i_n = $(i_n)
    $(av).size = $(size)

subcode:: sv_ref_release
    $case $(T:av)
        &call with_sv_av, sv
            $if $(list)
                $for i=$(i_0):$(i_n)
                    SV_refdec($(list)[i])
                free($(list))

fncode: SV_av_on_write(sv)
    $call assert, sv->type == SVt_av
    &call sv_on_write, sv
        &call with_sv_av, sv
            $my p_sv_new
            $allocate(n_len+20) p_sv_new
            memcpy(p_sv_new+10, p_sv, n_len*sizeof(struct SV*))
            $(i_0)=10
            $(i_n)=10+n_len
            $(size)=n_len+20
            
#---- AV functions ----------------------------
macros:
    AV_HEADROOM: 10
    av_count: ($1->i_n-$1->i_0)

fncode: AV_resize(av, n)
    # $call @count, av_resize
    $if n==0
        n=(av->i_n-av->i_0)*5/3+$(AV_HEADROOM)*2
    $elif n<0
        $local tb_neg=0
        tb_neg=1
        n=-n

    $if av->size==0
        # was empty
        $allocate(n) av->p_array
        av->i_0 = $(AV_HEADROOM)
        av->i_n = $(AV_HEADROOM)
        av->size = n
    $elif n < av->size
        $if av->i_0 > $(AV_HEADROOM)
            $call simply_shift
    $else
        $call allocate_new

    subcode: simply_shift
        $call copy, av->p_array

    subcode: allocate_new
        $my p_sv
        $allocate(n) p_sv
        $call copy, p_sv
        free(av->p_array)
        av->p_array=p_sv
        av->size =n

    subcode: copy(dst)
        $my p_sv_1, p_sv_2, struct SV** p_sv_n
        $if tb_neg
            $call copy_b
        $else
            $call copy_a

        subcode: copy_a
            p_sv_1=$(dst)+$(AV_HEADROOM)
            p_sv_2=av->p_array+av->i_0
            p_sv_n=av->p_array+av->i_n

            $while p_sv_2<p_sv_n
                *p_sv_1++ = *p_sv_2++

            av->i_0=$(AV_HEADROOM)
            av->i_n=(int)(p_sv_1-$(dst))

        subcode: copy_b
            p_sv_1=$(dst)+n-$(AV_HEADROOM)-1
            p_sv_2=av->p_array+av->i_n-1
            p_sv_n=av->p_array+av->i_0

            $while p_sv_2 >= p_sv_n
                *p_sv_1-- = *p_sv_2--

            av->i_0=(int)(p_sv_1-$(dst)+1)
            av->i_n=n-$(AV_HEADROOM)

#----------------------------------------------- 
fncode: AV_push(av, sv_value)
    $call @debug_AV_push
    $if av->i_n+1 >= av->size
        AV_resize(av, 0)
    av->p_array[av->i_n]=sv_value
    av->i_n++
    $if sv_value
        SV_refinc(sv_value)

# push/pop, caller responsible for ref_dec
fncode: AV_pop(av)
    $return_type struct SV *
    $if av->i_n>av->i_0
        av->i_n--
        return av->p_array[av->i_n]
    return NULL

fncode: AV_shift(av)
    $return_type struct SV *
    $if av->i_n>av->i_0
        av->i_0++
        return av->p_array[av->i_0-1]
    return NULL

fncode: AV_grow(av, n)
    $if av->i_n+n >= av->size
        size=(av->i_n-av->i_0)+n+$(AV_HEADROOM)*2
        AV_resize(av, size)

    memset(av->p_array+av->i_n, 0, sizeof(struct SV*)*n)
    av->i_n += n

fncode: AV_unshift(av, n)
    $if av->i_0 <= n
        size=(av->i_n-av->i_0)+n+$(AV_HEADROOM)*2
        AV_resize(av, -size)

    av->i_0 -= n
    memset(av->p_array+av->i_0, 0, sizeof(struct SV*)*n)

#----------------------------------------------- 
fncode: AV_get(av, i)
    $return_type struct SV*
    j=av->i_0+i
    # $print "AV_get $i <%d-%d> $j", av->i_0, av->i_n
    $if j <av->i_n
        return av->p_array[j]
    $else
        return NULL

fncode: AV_set(av, i, sv_value)
    j=av->i_0+i
    $if j<av->i_0
        AV_unshift(av, av->i_0-j)
    $elif j>=av->i_n
        AV_grow(av, j-av->i_n+1)
    av->p_array[av->i_0+j]=sv_value

#----------------------------------------------- 
subcode: av_each(av)
    $for $(av)->i_0:$(av)->i_n
        $(set:sv=$(av)->p_array[$(i)])
        $(set:i=$(i)-$(av)->i_0)
        BLOCK
